/* ----------------------------------------------------------------------------
** Team MagiDev
** © 2017 DigiPen Institute of Technology, All Rights Reserved.
**
** EnemyShoot.z
**
** Author:
** - Bar Ben-zvi
**
** Contributors:
** - Eli, Nelson
** --------------------------------------------------------------------------*/
//      10        20        30        40        50        60        70        80        90       100
class EnemyShoot : ZilchComponent
{
    [Property] var BulletDelay : Real = 1;
    [Property] var BulletSpeed : Real = 1000;
    [Property] var ShotPattern : ShotPatternID = ShotPatternID.SINGLE;
    [Property] var ShootSound : SoundCue = null;
    var FinishedAnimation : Boolean = false;
    [Property] var WalkAnimation : SpriteSource  = null;
    [Property] var AttackAnimation : SpriteSource  = null;
    var TimeUntilNextShot : Real = 1;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        this.TimeUntilNextShot -= event.Dt;
        var radianRotation = Math.ToRadians(this.Owner.Transform.Rotation.Z);
        if (this.Space.FindObjectByName("Player") == null)
        {
            return;
        }
        var shotAngle = Math.Normalize(this.Space.FindObjectByName("Player").Transform.Translation - this.Owner.Transform.Translation);
        if(this.TimeUntilNextShot <= 0)
        {
            
            if(this.ShootSound != null)
            {
                this.Space.SoundSpace.PlayCue(this.ShootSound);
            }
            if(this.AttackAnimation != null)
                this.Owner.Sprite.SpriteSource = this.AttackAnimation;
            this.TimeUntilNextShot = this.BulletDelay;
            this.Owner.Shoot.Shoot(this.BulletSpeed, shotAngle, this.ShotPattern);
        }
        if(this.Owner.Sprite.SpriteSource == this.AttackAnimation)
        {
            if(this.Owner.Sprite.CurrentFrame >= this.AttackAnimation.FrameCount - 1)
                this.FinishedAnimation = true;
            if(this.Owner.Sprite.CurrentFrame == 0 && this.FinishedAnimation)
            {
                this.FinishedAnimation = false;
                
                this.Owner.Sprite.SpriteSource = this.WalkAnimation;
            }
        }
    }
}
