class MusicManager : ZilchComponent
{
    var MusicSpace : Space = null;
    
    var CurrentEmitter : SoundEmitter = null;
    [Property]
    var PlayOnInitialize : Boolean = true;
    [Property]
    var NextCue : SoundCue = null;
    
    var CurrentEmitterIsPlaying : Boolean = false;
    
    [Property]
    var FadeTime : Real = 2;
    
    [Property]
    var MusicVolume : Real = 1;
    
    var TimeSinceFadeStart : Real = 0;
    
    function Initialize(init : CogInitializer)
    {
        this.MusicSpace = this.GameSession.CreateNamedSpace("MusicSpace", Archetype.Space);
        this.MusicSpace.LoadLevel(Level.MusicLevel);
        Zero.Connect(this.MusicSpace, Events.LogicUpdate, this.OnLogicUpdate);
        
        this.CurrentEmitter = this.MusicSpace.FindObjectByName("Camera").SoundEmitter;
        
        if (this.PlayOnInitialize)
        {
            this.SwitchTracks(this.NextCue);
        }
        else
        {
            this.NextCue = null;
        }
    }

    
    function OnLogicUpdate(event : UpdateEvent)
    {
        // Are we trying to fade?
        if (this.NextCue != null)
        {
            this.TimeSinceFadeStart += event.Dt;
            this.CurrentEmitter.Volume = Math.Lerp(this.MusicVolume, 0, this.TimeSinceFadeStart / this.FadeTime);
            
            if (this.CurrentEmitter.Volume <= 0)
            {
                this.CurrentEmitter.Volume = this.MusicVolume;
                this.CurrentEmitter.Stop();
                this.CurrentEmitter.PlayCue(this.NextCue);
                this.NextCue = null;
            }
        }
    }

    function SwitchTracks(cue : SoundCue)
    {
        if (this.CurrentEmitterIsPlaying == false)
        {
            this.CurrentEmitter.PlayCue(cue);
            this.CurrentEmitterIsPlaying = true;
            this.NextCue = null;
        }
        else
        {
            this.NextCue = cue;
            this.TimeSinceFadeStart = 0;
        }
    }

}
